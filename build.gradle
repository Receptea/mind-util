import groovy.json.JsonSlurper

apply plugin: 'java'

group 'com.github.iarkn'
version '1.0'

ext {
    def json = new JsonSlurper()
    def mod = json.parse(new File('mod.json'))

    artifactName = "${mod.name}-${mod.version}"
    sdkRoot = System.getenv('ANDROID_SDK_ROOT') ?: System.getenv('ANDROID_HOME')
    // For the special operating system
    requiresSpecialTreatment = System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    annotationProcessor 'com.github.Anuken:jabel:368c91431d9388d0fec7e1a449a0ce3da03b56e8'
    compileOnly "com.github.Anuken.Mindustry:core:$gameVersion"
    compileOnly "com.github.Anuken.Arc:arc-core:$gameVersion"
}

jar {
    archiveFileName = "$artifactName-desktop.jar"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    from(rootDir) {
        include 'mod.json'
    }
}

task dex {
    dependsOn 'jar'

    doFirst {
        exec {
            workingDir "$buildDir/libs"

            // Collect dependencies needed for desugaring
            def files = (
                configurations.compileClasspath.asList() + configurations.runtimeClasspath.asList() +
                [new File("$sdkRoot/platforms/android-$apiLevel/android.jar")]
            )
            // d8 <classpath ...> --min-api 14 --output <dex.jar> <desktop.jar>
            def args = [
                'd8', *files.collect { ['--classpath', it.path] }.flatten(),
                '--min-api', '14', '--output', "$artifactName-dex.jar", "$artifactName-desktop.jar"
            ]

            // Dex and desugar files -- this requires 'd8' in your PATH environment variable
            if (requiresSpecialTreatment) {
                commandLine 'cmd', '/c', args
            } else {
                commandLine args
            }
        }
    }
}

task deploy(type: Jar) {
    dependsOn 'jar', 'dex'

    archiveFileName = "${artifactName}.jar"

    from(
        zipTree("$buildDir/libs/$artifactName-desktop.jar"),
        zipTree("$buildDir/libs/$artifactName-dex.jar")
    )

    doLast {
        delete {
            delete "$buildDir/libs/$artifactName-desktop.jar"
            delete "$buildDir/libs/$artifactName-dex.jar"
        }
    }
}

tasks.withType(JavaCompile) {
    sourceCompatibility = 16
    targetCompatibility = 8

    options.encoding = 'UTF-8'
    options.fork = true
    // If you are not using JDK >= 9, this will not work. But you should be using JDK 16 anyway
    options.compilerArgs += ['-Xlint:all', '--release', '8']
}
